plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'com.example.mydemo2'
    compileSdk 35

    // 添加 flavor dimensions
    flavorDimensions "default"

    defaultConfig {
        applicationId "com.example.mydemo2"
        minSdk 24
        targetSdk 35
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 读取命令行参数，如果没有提供则使用默认值
        def ch = project.hasProperty('ch') ? project.property('ch') : "default_value"
        def webUrl = project.hasProperty('webUrl') ? project.property('webUrl') : ""

        // 将参数添加到BuildConfig中
        buildConfigField "String", "ch", "\"${ch}\""
        buildConfigField "String", "webUrl", "\"${webUrl}\""
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def customOutputName = project.hasProperty("outputFileName")
                    ? project.property("outputFileName")
                    : "app-${variant.buildType.name}.apk"

            outputFileName = customOutputName
        }
    }

    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }
    buildFeatures {
        buildConfig  true
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    // 添加产品风味配置
    productFlavors {
        channel {
            dimension "default"
            applicationIdSuffix project.hasProperty('ch') ? ".${project.property('ch')}" : ""
        }
    }
}

dependencies {

    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}